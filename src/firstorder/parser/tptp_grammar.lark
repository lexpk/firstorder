tptp_file : tptp_input*
tptp_input : annotated_formula | include
annotated_formula : fof_annotated | cnf_annotated
fof_annotated : "fof(" NAME "," FORMULA_ROLE "," fof_formula annotations* ")."
cnf_annotated : "cnf(" NAME "," FORMULA_ROLE "," cnf_formula annotations* ")."
annotations : "," source (optional_info)*
FORMULA_ROLE : "axiom" | "hypothesis" | "definition" | "assumption" | "lemma" | "theorem" | "corollary" | "conjecture"
            | "negated_conjecture" | "plain" | "type" | "fi_domain" | "fi_functors" | "fi_predicates" | "unknown" | LOWER_WORD
fof_formula : fof_logic_formula | fof_sequent
fof_logic_formula : fof_binary_formula | fof_unitary_formula
fof_binary_formula :  fof_binary_nonassoc | fof_binary_assoc
fof_binary_nonassoc : fof_unitary_formula BINARY_CONNECTIVE fof_unitary_formula
fof_binary_assoc : fof_or_formula | fof_and_formula
fof_or_formula : fof_unitary_formula "|" fof_unitary_formula | fof_or_formula "|" fof_unitary_formula
fof_and_formula  : fof_unitary_formula "&" fof_unitary_formula | fof_and_formula "&"  fof_unitary_formula
fof_unitary_formula : fof_quantified_formula | fof_unary_formula | fof_atomic_formula | "(" fof_logic_formula ")"
fof_quantified_formula : FOF_QUANTIFIER "[" fof_variable_list "]" whitespace ":" fof_unitary_formula
whitespace : WS*
fof_variable_list : VARIABLE ("," VARIABLE)*
fof_unary_formula : "~" fof_unitary_formula | fof_infix_unary
fof_infix_unary : fof_term INFIX_INEQUALITY fof_term
fof_atomic_formula : fof_plain_atomic_formula | fof_defined_atomic_formula | fof_system_atomic_formula
fof_plain_atomic_formula : fof_plain_term
fof_defined_atomic_formula : fof_defined_plain_formula | fof_defined_infix_formula
fof_defined_plain_formula : fof_defined_plain_term | DEFINED_PROPOSITION | DEFINED_PREDICATE "(" fof_arguments ")"
fof_defined_infix_formula : fof_term defined_infix_pred fof_term
fof_system_atomic_formula : fof_system_term
fof_plain_term : CONSTANT | FUNCTOR "(" fof_arguments ")"
fof_defined_term : defined_term | fof_defined_atomic_term
fof_defined_atomic_term : fof_defined_plain_term
fof_defined_plain_term : DEFINED_CONSTANT | DEFINED_FUNCTOR "(" fof_arguments ")"
fof_system_term : SYSTEM_CONSTANT | SYSTEM_FUNCTOR "(" fof_arguments ")"
fof_arguments : fof_term ("," fof_term)*
fof_term : fof_function_term | VARIABLE
fof_function_term : fof_plain_term | fof_defined_term | fof_system_term
fof_sequent : fof_formula_tuple "-->" fof_formula_tuple | "(" fof_sequent ")"
fof_formula_tuple : "[" [fof_formula_tuple_list] "]"
fof_formula_tuple_list : fof_logic_formula ("," fof_logic_formula)*
cnf_formula : disjunction | "(" disjunction ")"
disjunction : literal ("|" literal)*
literal : fof_atomic_formula | negated_fof_atomic_formula | fof_infix_unary
negated_fof_atomic_formula : "~" fof_atomic_formula
FOF_QUANTIFIER : "!" | "?"
BINARY_CONNECTIVE : "<=>" | "=>" | "<=" | "<~>" | "~|" | "~&"
ASSOC_CONNECTIVE: "&" | "|"
DEFINED_PROPOSITION :  ATOMIC_DEFINED_WORD | "$true" | "$false"
DEFINED_PREDICATE : ATOMIC_DEFINED_WORD | "$distinct" | "$less" | "$lesseq" | "$greater" | "$greatereq" | "$is_int"
                    | "$is_rat" | "$box_P" | "$box_i" | "$box_int" | "$box" | "$dia_P" | "$dia_i" | "$dia_int" | "$dia"
defined_infix_pred : INFIX_EQUALITY
INFIX_EQUALITY : "="
INFIX_INEQUALITY : "!="
CONSTANT : FUNCTOR
FUNCTOR : ATOMIC_WORD
SYSTEM_CONSTANT : SYSTEM_FUNCTOR
SYSTEM_FUNCTOR : ATOMIC_SYSTEM_WORD
DEFINED_CONSTANT : DEFINED_FUNCTOR
DEFINED_FUNCTOR : ATOMIC_DEFINED_WORD |"$uminus" | "$sum" | "$difference" | "$product" | "$quotient" | "$quotient_e" | "$quotient_t" | "$quotient_f"
                | "$remainder_e" | "$remainder_t" | "$remainder_f" | "$floor" | "$ceiling" | "$truncate" | "$round" | "$to_int" | "$to_rat" | "$to_real"
defined_term : number | DISTINCT_OBJECT
VARIABLE : UPPER_WORD
source : general_term | dag_source | internal_source | external_source | "[" sources "]"
sources : source ("," source)*
dag_source : NAME | inference_record
inference_record : "inference(" INFERENCE_RULE "," useful_info "," inference_parents ")"
INFERENCE_RULE : ATOMIC_WORD
inference_parents : "[" parent_list* "]"
parent_list : parent_info ("," parent_info)*
parent_info : source parent_details*
parent_details : general_list
internal_source : "introduced(" intro_type optional_info* ")"
intro_type : "definition" | "axiom_of_choice" | "tautology" | "assumption"
external_source : file_source | theory | creator_source
file_source : "file(" FILE_NAME FILE_INFO* ")"
FILE_INFO : "," NAME
theory : "theory(" THEORY_NAME optional_info* ")"
THEORY_NAME : "equality" | "ac"
creator_source : "creator(" CREATOR_NAME optional_info* ")"
CREATOR_NAME : ATOMIC_WORD
optional_info : "," useful_info
useful_info : general_list | "[" info_items* "]"
info_items : info_item ("," info_item)*
info_item : formula_item | inference_item | general_function
formula_item : DESCRIPTION_ITEM  | IQUOTE_ITEM
DESCRIPTION_ITEM : "description(" ATOMIC_WORD ")"
IQUOTE_ITEM : "iquote(" ATOMIC_WORD ")"
inference_item : inference_status | assumptions_record | new_symbol_record | refutation
inference_status : "status(" STATUS_VALUE ")" | inference_info
STATUS_VALUE : "suc" | "unp" | "sap" | "esa" | "sat" | "fsa" | "thm" | "eqv" | "tac" | "wec" | "eth" | "tau" | "wtc" | "wth" | "cax" | "sca" | "tca"
            | "wca" | "cup" | "csp" | "ecs" | "csa" | "cth" | "ceq" | "unc" | "wcc" | "ect" | "fun" | "uns" | "wuc" | "wct" | "scc" | "uca" | "noc"
inference_info : INFERENCE_RULE "(" ATOMIC_WORD "," general_list ")"
assumptions_record : "assumptions([" name_list "])"
refutation : "refutation(" file_source ")"
new_symbol_record : "new_symbols(" ATOMIC_WORD ", [" new_symbol_list "])"
new_symbol_list : principal_symbol ("," principal_symbol)*
principal_symbol : FUNCTOR | VARIABLE
include : "include(" FILE_NAME formula_selection* ")."
formula_selection : ",[" name_list "]"
name_list : NAME ("," NAME)*
general_term : general_data | general_data ":" general_term | general_list
general_data : ATOMIC_WORD | general_function | VARIABLE | number | DISTINCT_OBJECT | formula_data | "bind(" VARIABLE "," formula_data ")"
general_function : ATOMIC_WORD "(" general_terms ")"
formula_data : "$fof(" fof_formula ")" | "$cnf(" cnf_formula ")"
general_list : "[" general_terms? "]"
general_terms : general_term ("," general_term)*
NAME : ATOMIC_WORD | INTEGER
ATOMIC_WORD : LOWER_WORD | SINGLE_QUOTED
ATOMIC_DEFINED_WORD : "$" LOWER_WORD
ATOMIC_SYSTEM_WORD : "$$" LOWER_WORD
number : INTEGER | RATIONAL | REAL
FILE_NAME : SINGLE_QUOTED
COMMENT_LINE : "%" (PRINTABLE_CHAR)* "\n"
COMMENT_BLOCK : "/" "*"+ (NOT_STAR | WS | "\n")* "*"+ "/"
NOT_STAR : /[^*]/
SINGLE_QUOTED : "'" (SQ_CHAR | WS | "\\'")+ "'"
DISTINCT_OBJECT : "\"" DO_CHAR* "\""
UPPER_WORD : UPPER_ALPHA ALPHA_NUMERIC*
LOWER_WORD : LOWER_ALPHA ALPHA_NUMERIC*
REAL : SIGN? DECIMAL_FRACTION | SIGN? DECIMAL_EXPONENT
RATIONAL : SIGN? DECIMAL "/" POSITIVE_DECIMAL
INTEGER : SIGN? DECIMAL
DECIMAL : ZERO_NUMERIC | POSITIVE_DECIMAL
POSITIVE_DECIMAL : NON_ZERO_NUMERIC NUMERIC*
DECIMAL_EXPONENT : DECIMAL "Ee" EXP_INTEGER | DECIMAL_FRACTION "Ee" EXP_INTEGER
DECIMAL_FRACTION : DECIMAL DOT_DECIMAL
DOT_DECIMAL : "." NUMERIC+
EXP_INTEGER : SIGN? NUMERIC+
DO_CHAR : /[(-~]/
SQ_CHAR : /[(-~]/
SIGN : "+" | "-"
ZERO_NUMERIC : "0"
NON_ZERO_NUMERIC : "1" .. "9"
NUMERIC : "0" .. "9"
LOWER_ALPHA : "a" .. "z"
UPPER_ALPHA : "A" .. "Z"
ALPHA_NUMERIC : LOWER_ALPHA | UPPER_ALPHA | NUMERIC | "_"
PRINTABLE_CHAR : /[ -~]/
VIEWABLE_CHAR : "\n"
%ignore COMMENT_LINE
%ignore COMMENT_BLOCK
%import common.WS
%ignore WS